grammar org.palladiosimulator.qes.QualityEffectSpecification with org.eclipse.xtext.common.Terminals

generate qualityEffectSpecification "http://www.palladiosimulator.org/qes/QualityEffectSpecification"

Model:
    specifications+=QES+;

QES:
    'For{' components+=ComponentSpecification+ '}' 'Do{' transformations+=TransformationSpecification+ '}';

ComponentSpecification:
    'Component(' properties+=ComponentProperty+ ')';

ComponentProperty:
    Name | Identifier | Annotation | Type | Role | Assembly | Resource;

Name:
    'Name(' not?='Not'? name=STRING ')';

Identifier:
    'Id(' not?='Not'? id=ID ')';

Annotation:
    'Annotation(' not?='Not'? annotation=STRING ')';

Type:
    'Type(' not?='Not'? type=ComponentType ')';

enum ComponentType:
    ANY='AnyComponentType' | BASIC='Basic' | COMPOSITE='Composite';

Role:
    'Role(' not?='Not'? type=RoleType properties+=RoleProperty+ ')';

enum RoleType:
    ANY='AnyRoleType' | COMPONENT_REQUIRED_PROVIDED='ComponentRequiredProvided' | COMPONENT_REQUIRED='ComponentRequired' | COMPONENT_PROVIDED='ComponentProvided' | INFRASTRUCTURE_REQUIRED_PROVIDED='InfrastructureRequiredProvided' | INFRASTRUCTURE_REQUIRED='InfrastructureRequired' | INFRASTRUCTURE_PROVIDED='InfrastructureProvided';

RoleProperty:
    Name | Identifier | Annotation;

Assembly:
    'Assembly(' not?='Not'? type=AssemblyType components+=ComponentSpecification+ ')';

enum AssemblyType:
    ANY='AnyAssembly' | REQUIRED='Required' | PROVIDED='Provided';

Resource:
    'Resource(' properties+=ResourceProperty+ ')';

ResourceProperty:
    Name | Identifier;

TransformationSpecification:
    NQA | Reasoning | Cost;

NQA:
    'NQA(' quality=STRING type=TransformationType element=STRING ')';

enum TransformationType:
    IS='=' | PLUS='+' | MINUS='-' | MULTIPLICATION='*' | DIVISION='/' ;

Reasoning:
    'Reasoning(' quality=STRING ':' rules+=Rule+ ')';

Rule:
    '(' qualities+=STRING+ ':' entries+=Entry+ ')';

Entry:
    '[' key+=STRING+ ':' value=STRING ']';

Cost:
     'Cost(' type=TransformationType cost=INT ')';
